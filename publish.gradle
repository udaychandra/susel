apply plugin: "maven-publish"
apply plugin: "signing"

javadoc {
    options.addBooleanOption('html5', true)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = "javadoc"
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "" + project.ext.pomName.toString()
                description = "" + project.ext.pomDescription.toString()
                url = "https://github.com/udaychandra/susel"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "udaychandra"
                        name = "Uday Tatiraju"
                        email = "ud.chandra@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/udaychandra/susel.git"
                    developerConnection = "scm:git:ssh://github.com/udaychandra/susel.git"
                    url = "https://github.com/udaychandra/susel"
                }
            }

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }
            
            artifact(sourcesJar) {
                classifier = "sources"
            }
            artifact(javadocJar) {
                classifier = "javadoc"
            }
            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username "$sonatypeUsername"
                password "$sonatypePassword"
            }
        }
    }
}

signing {
    sign configurations.archives
}
